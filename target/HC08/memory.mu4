( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2009 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

cr  " HC08 memory image " file[#

( Space for compiling into. HC08s can get big; for now let's limit ourselves
  to 60k address space. This is where most of the varieties I'm interested
  in max out.

  We should really define this in a device/xxx file that gets loaded
  _before_ this one.)

60 Ki constant #image
#image buffer 'image
: |+  'image + ;

( HC08s are big-endian, like all Motorola/Freescale processors!)

-:  |+ c@ ;                                        is |c@
-:  dup |c@ ( hi) 8 <<  swap 1+ |c@ ( lo)  or ;    is |@
2 |cell !  ( size of cell)

: |c!  |+ c! ;
: |!  ( w a)  push  dup 8 >> r@ ( hi) |c!  pop 1+ ( lo) |c! ;

( Dictionary operators.)
variable |h   ( image dictionary pointer)

( XXX - necessary?)
: |aligned  1 + -2 and ;
: |align  |h @ |aligned |h ! ;

: .h  radix @ push  hex <# # # #> type space  pop radix ! ;
: |here  ( - a)  |h @  ;
: |c,    ( ch)   dup .h  |here |c!  1 |h +!  ;
: |,     ( n)    ( |align)  dup 8 >> |c, ( hi)  |c, ( lo) ;
: |#     ( - n)  |here |+  'image -  ;  ( return the number of bytes in image)

( Initialization.)
: wipe      'image  #image  ( erase)   "55  fill  ;
: org   ( buffer len org)  nip nip  ( for now)  |h ! ;

wipe  'image #image  0  org

#]file


